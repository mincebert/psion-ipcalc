IPSUBNET:
REM 0.49 2017-01-07
GLOBAL net%(4),mask%(4),masklen%,ip%(4)
LOCAL exist%,addr%(4),scheme$(6),addr$(15),octet%,mask$(15),hmask$(15),menu$(200),addrs,addrs$(4),opt%,gwopt%,bit%,found%
exist%=EXIST("A:IPSUBNET")
IF exist%
 OPEN "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,scheme$
 addr%(1)=A.addr1%
 addr%(2)=A.addr2%
 addr%(3)=A.addr3%
 addr%(4)=A.addr4%
 masklen%=A.masklen%
 scheme$=A.scheme$
 CLOSE
ELSE
 addr%(1)=192
 addr%(2)=168
 addr%(3)=100
 addr%(4)=1
 masklen%=23
 scheme$="Top"
ENDIF
DO
 CLS
 PRINT "Busy... ";CHR$(91);"-------";CHR$(93)
 AT 10,1
 addr$=IPSTR$:(addr%(1),addr%(2),addr%(3),addr%(4))
 menu$="IP   "+addr$
 PRINT "I";
 octet%=1
 DO
  IF masklen%>=(octet%*8)
   mask%(octet%)=255
  ELSEIF masklen%<=((octet%-1)*8)
   mask%(octet%)=0
  ELSE
   mask%(octet%)=256-2**(octet%*8-masklen%)
  ENDIF
  octet%=octet%+1
 UNTIL octet%>4
 mask$=IPSTR$:(mask%(1),mask%(2),mask%(3),mask%(4))
 menu$=menu$+",Mask "+mask$
 PRINT "M";
 octet%=1
 DO
  ip%(octet%)=255-mask%(octet%)
  octet%=octet%+1
 UNTIL octet%>4
 hmask$=IPSTR$:(ip%(1),ip%(2),ip%(3),ip%(4))
 menu$=menu$+",Hmsk "+hmask$
 PRINT "H";
 addrs=2.0**(32-masklen%)
 IF addrs>1E9
  addrs$=GEN$(INT(addrs/1E9),3)+"B"
 ELSEIF addrs>1E6
  addrs$=GEN$(INT(addrs/1E6),3)+"M"
 ELSEIF addrs>1E4
  addrs$=GEN$(INT(addrs/1E3),3)+"k"
 ELSE
  addrs$=GEN$(INT(addrs+0.5),4)
 ENDIF
 menu$=menu$+",Len  /"+GEN$(masklen%,2)+"  "+addrs$+" addr"
 IF addrs>1
  menu$=menu$+"s"
 ENDIF
 PRINT "L";
 octet%=1
 DO
  net%(octet%)=addr%(octet%) AND mask%(octet%)
  octet%=octet%+1
 UNTIL octet%>4
 menu$=menu$+",=net "+IPSTR$:(net%(1),net%(2),net%(3),net%(4))
 PRINT "N";
 menu$=menu$+",=b/c "+IPCALC$:(-1)
 PRINT "B";
 menu$=menu$+",Gw   "
 IF scheme$="No"
  menu$=menu$+"no scheme"
 ELSE
  menu$=menu$+""""+scheme$+""" scheme"
 ENDIF
 PRINT "G";
 IF scheme$="Top"
  menu$=menu$+",=gw  "+IPCALC$:(-2)
  menu$=menu$+",=r1  "+IPCALC$:(-3)
  menu$=menu$+",=r2  "+IPCALC$:(-4)
 ELSEIF scheme$="Bottom"
  menu$=menu$+",=gw  "+IPCALC$:(1)
  menu$=menu$+",=r1  "+IPCALC$:(2)
  menu$=menu$+",=r2  "+IPCALC$:(3)
 ELSEIF scheme$="62"
  menu$=menu$+",=gw  "+IPCALC$:(62)
  menu$=menu$+",=r1  "+IPCALC$:(-2)
  menu$=menu$+",=r2  "+IPCALC$:(61)
 ELSEIF scheme$="Alt-62"
  menu$=menu$+",=gw  "+IPCALC$:(62)
  menu$=menu$+",=r1  "+IPCALC$:(-2)
  menu$=menu$+",=r2  "+IPCALC$:(-3)
 ELSEIF scheme$="Bottop"
  menu$=menu$+",=gw  "+IPCALC$:(1)
  menu$=menu$+",=r1  "+IPCALC$:(-2)
  menu$=menu$+",=r2  "+IPCALC$:(-3)
 ELSEIF scheme$="254"
  menu$=menu$+",=gw  "+IPCALC$:(254)
  menu$=menu$+",=r1  "+IPCALC$:(253)
  menu$=menu$+",=r2  "+IPCALC$:(252)
 ELSEIF scheme$="P-t-P"
  menu$=menu$+",=r1 "+IPCALC$:(1)
  menu$=menu$+",=r2 "+IPCALC$:(-2)
 ENDIF
 KEY
 DO
  opt%=MENU(menu$)
  IF opt%=1
   IPINPUT:("IP address",addr$)
   octet%=1
   DO
    addr%(octet%)=ip%(octet%)
    octet%=octet%+1
   UNTIL octet%>4
   BREAK
  ELSEIF (opt%>=2) AND (opt%<=4)
   IF opt%=2
    IPINPUT:("Netmask",mask$)
    masklen%=32
    octet%=4
    found%=0
    DO
     bit%=0
     DO
      found%=(ip%(octet%) AND INT(2**bit%))<>0
      IF NOT(found%)
       masklen%=masklen%-1
       bit%=bit%+1
      ENDIF
     UNTIL found% OR (bit%>7)
     octet%=octet%-1
    UNTIL octet%<1
   ELSEIF opt%=3
    IPINPUT:("Hostmask (Cisco)",hmask$)
    masklen%=32
    octet%=4
    found%=0
    DO
     bit%=0
     DO
      found%=(ip%(octet%) AND INT(2**bit%))=0
      IF NOT(found%)
       masklen%=masklen%-1
       bit%=bit%+1
      ENDIF
     UNTIL found% OR (bit%>7)
     octet%=octet%-1
    UNTIL octet%<1
   ELSEIF opt%=4
    PRINT "Netmask length"+CHR$(63)
    INPUT masklen%
    masklen%=MIN(MAX(masklen%,0),32)
   ENDIF
   IF (masklen%=30) AND (scheme$<>"P-t-P")
    CLS
    PRINT "Gateway scheme set"
    PRINT "to ""P-t-P""."
    scheme$="P-t-P"
    PAUSE -20
   ENDIF
   BREAK
  ELSEIF opt%=7
   PRINT "Gateway scheme"+CHR$(63)
   gwopt%=MENUN(2,"62,Top,Bottom,254,Alt-62,Bottop,P-t-P,None")
   IF gwopt%=1
    scheme$="62"
   ELSEIF gwopt%=2
    scheme$="Top"
   ELSEIF gwopt%=3
    scheme$="Bottom"
   ELSEIF gwopt%=4
    scheme$="254"
   ELSEIF gwopt%=5
    scheme$="Alt-62"
   ELSEIF gwopt%=6
    scheme$="Bottop"
   ELSEIF gwopt%=7
    scheme$="P-t-P"
    IF masklen%<>30
     PRINT "Netmask length set"
     PRINT "to /30."
     masklen%=30
     PAUSE -20
    ENDIF
   ELSEIF gwopt%=8
    scheme$="No"
   ENDIF
   BREAK
  ELSEIF opt%<>0
   PRINT "Not editable."
   PAUSE -10
  ENDIF
 UNTIL opt%=0
UNTIL opt%=0
IF exist%
 OPEN "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,scheme$
ELSE
 CREATE "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,scheme$
ENDIF
A.addr1%=addr%(1)
A.addr2%=addr%(2)
A.addr3%=addr%(3)
A.addr4%=addr%(4)
A.masklen%=masklen%
A.scheme$=scheme$
IF exist%
 UPDATE
ELSE
 APPEND
ENDIF
CLOSE
