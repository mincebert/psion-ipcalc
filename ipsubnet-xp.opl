IPSUBNET:
REM 0.54-xp 2017-05-01
GLOBAL net%(4),mask%(4),masklen%,ip%(4)
LOCAL exist%,addr%(4),router$(6),addr$(15),menu$(176),octet%,mask$(15),hmask$(15),hexaddr$(8),addrs,addrs$(4),opt%,rtropt%,bit%,found%
exist%=EXIST("A:IPSUBNET")
IF exist%
 OPEN "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,router$
 addr%(1)=A.addr1%
 addr%(2)=A.addr2%
 addr%(3)=A.addr3%
 addr%(4)=A.addr4%
 masklen%=A.masklen%
 router$=A.router$
 CLOSE
ELSE
 addr%(1)=192
 addr%(2)=168
 addr%(3)=100
 addr%(4)=1
 masklen%=23
 router$="Top"
ENDIF
DO
 CLS
 PRINT "Busy..."
 PRINT CHR$(91);"---------";CHR$(93)
 AT 2,2
 PRINT "I";
 addr$=IPSTR$:(addr%(1),addr%(2),addr%(3),addr%(4))
 menu$="I"+addr$
 PRINT "M";
 octet%=1
 DO
  IF masklen%>=(octet%*8)
   mask%(octet%)=255
  ELSEIF masklen%<=((octet%-1)*8)
   mask%(octet%)=0
  ELSE
   mask%(octet%)=256-2**(octet%*8-masklen%)
  ENDIF
  octet%=octet%+1
 UNTIL octet%>4
 mask$=IPSTR$:(mask%(1),mask%(2),mask%(3),mask%(4))
 menu$=menu$+",M"+mask$
 PRINT "H";
 octet%=1
 DO
  ip%(octet%)=255-mask%(octet%)
  octet%=octet%+1
 UNTIL octet%>4
 hmask$=IPSTR$:(ip%(1),ip%(2),ip%(3),ip%(4))
 menu$=menu$+",H"+hmask$
 PRINT "L";
 addrs=2.0**(32-masklen%)
 IF addrs>1E9
  addrs$=GEN$(INT(addrs/1E9),3)+"B"
 ELSEIF addrs>1E6
  addrs$=GEN$(INT(addrs/1E6),3)+"M"
 ELSEIF addrs>1E4
  addrs$=GEN$(INT(addrs/1E3),3)+"k"
 ELSE
  addrs$=GEN$(INT(addrs+0.5),4)
 ENDIF
 menu$=menu$+",L/"+GEN$(masklen%,2)+":"+addrs$+"-addr"
 IF addrs>1
  menu$=menu$+"s"
 ENDIF
 PRINT "N";
 octet%=1
 DO
  net%(octet%)=addr%(octet%) AND mask%(octet%)
  octet%=octet%+1
 UNTIL octet%>4
 menu$=menu$+",="+IPSTR$:(net%(1),net%(2),net%(3),net%(4))
 PRINT "B";
 menu$=menu$+",*"+IPCALC$:(-1)
 PRINT "X";
 hexaddr$=IHXSTR$:(addr%(1),addr%(2),addr%(3),addr%(4))
 menu$=menu$+","+"X"+hexaddr$
 PRINT "R";
 menu$=menu$+",R"
 IF router$="No"
  menu$=menu$+"no-scheme"
 ELSE
  menu$=menu$+""""+router$+"""-scheme"
 ENDIF
 PRINT "G";
 IF router$="Top"
  menu$=menu$+",-"+IPCALC$:(-2)
  menu$=menu$+",<"+IPCALC$:(-3)
  menu$=menu$+",>"+IPCALC$:(-4)
 ELSEIF router$="Bottom"
  menu$=menu$+",-"+IPCALC$:(1)
  menu$=menu$+",<"+IPCALC$:(2)
  menu$=menu$+",>"+IPCALC$:(3)
 ELSEIF router$="62"
  menu$=menu$+",-"+IPCALC$:(62)
  menu$=menu$+",<"+IPCALC$:(-2)
  menu$=menu$+",>"+IPCALC$:(61)
 ELSEIF router$="Alt-62"
  menu$=menu$+",-"+IPCALC$:(62)
  menu$=menu$+",<"+IPCALC$:(-2)
  menu$=menu$+",>"+IPCALC$:(-3)
 ELSEIF router$="Bottop"
  menu$=menu$+",-"+IPCALC$:(1)
  menu$=menu$+",<"+IPCALC$:(-2)
  menu$=menu$+",>"+IPCALC$:(-3)
 ELSEIF router$="254"
  menu$=menu$+",-"+IPCALC$:(254)
  menu$=menu$+",<"+IPCALC$:(253)
  menu$=menu$+",>"+IPCALC$:(252)
 ELSEIF router$="P-t-P"
  menu$=menu$+",<"+IPCALC$:(1)
  menu$=menu$+",>"+IPCALC$:(-2)
 ENDIF
 KEY
 DO
  opt%=MENU(menu$)
  IF opt%=1
   IPINPUT:("IP address",addr$)
   octet%=1
   DO
    addr%(octet%)=ip%(octet%)
    octet%=octet%+1
   UNTIL octet%>4
   BREAK
  ELSEIF (opt%>=2) AND (opt%<=4)
   IF opt%=2
    IPINPUT:("Netmask",mask$)
    masklen%=32
    octet%=4
    found%=0
    DO
     bit%=0
     DO
      found%=(ip%(octet%) AND INT(2**bit%))<>0
      IF NOT(found%)
       masklen%=masklen%-1
       bit%=bit%+1
      ENDIF
     UNTIL found% OR (bit%>7)
     octet%=octet%-1
    UNTIL octet%<1
   ELSEIF opt%=3
    IPINPUT:("Hostmask",hmask$)
    masklen%=32
    octet%=4
    found%=0
    DO
     bit%=0
     DO
      found%=(ip%(octet%) AND INT(2**bit%))=0
      IF NOT(found%)
       masklen%=masklen%-1
       bit%=bit%+1
      ENDIF
     UNTIL found% OR (bit%>7)
     octet%=octet%-1
    UNTIL octet%<1
   ELSEIF opt%=4
    DO
     CLS
     PRINT "Netmask length"+CHR$(63)
     INPUT masklen%
     IF (masklen%<0) OR (masklen%>32)
      CLS
      PRINT "Error: masklen"
      PRINT "out of range.";CHR$(16)
      PAUSE -20
     ENDIF
    UNTIL (masklen%>=0) AND (masklen%<=32)
   ENDIF
   IF (masklen%=30) AND (router$<>"P-t-P")
    CLS
    PRINT "Gateway scheme"
    PRINT "set to ""P-t-P""."
    router$="P-t-P"
    PAUSE -20
   ENDIF
   BREAK
  ELSEIF opt%=7
   IHXINPUT:("Hex IP address",hexaddr$)
   octet%=1
   DO
    addr%(octet%)=ip%(octet%)
    octet%=octet%+1
   UNTIL octet%>4
   BREAK
  ELSEIF opt%=8
   rtropt%=MENU("62,Alt-62,254,Top,Bottom,Bottop,P-t-P,None")
   IF rtropt%=1
    router$="62"
   ELSEIF rtropt%=2
    router$="Alt-62"
   ELSEIF rtropt%=3
    router$="254"
   ELSEIF rtropt%=4
    router$="Top"
   ELSEIF rtropt%=5
    router$="Bottom"
   ELSEIF rtropt%=6
    router$="Bottop"
   ELSEIF rtropt%=7
    router$="P-t-P"
    IF masklen%<>30
     PRINT "Netmask length"
     PRINT "set to /30."
     masklen%=30
     PAUSE -20
    ENDIF
   ELSEIF rtropt%=8
    router$="No"
   ENDIF
   BREAK
  ELSEIF opt%<>0
   PRINT "Not editable."
   PAUSE -10
  ENDIF
 UNTIL opt%=0
UNTIL opt%=0
IF exist%
 OPEN "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,router$
ELSE
 CREATE "A:IPSUBNET",A,addr1%,addr2%,addr3%,addr4%,masklen%,router$
ENDIF
A.addr1%=addr%(1)
A.addr2%=addr%(2)
A.addr3%=addr%(3)
A.addr4%=addr%(4)
A.masklen%=masklen%
A.router$=router$
IF exist%
 UPDATE
ELSE
 APPEND
ENDIF
CLOSE
